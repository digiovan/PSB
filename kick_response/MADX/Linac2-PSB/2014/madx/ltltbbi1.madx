/*****************************************************
 *
 * MADX file for the LT-LTB line from LINAC2 to the PS BOOSTER
 *
 * Directory: /afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/
 *
 * Execute with:  >madx < ltltbbi1.madx
 * This file is for Protons at 311 MeV/c (Ekin=50 MeV, BRHO=1.03527 "the magnetic rigidity of the protons")
 *****************************************************/


/*****************************************************************************
 * TITLE
 *****************************************************************************/
 title, 'LT-LTB-BI 2014 optics. Protons - 311 MeV/c (E_kin=50 MeV)';

 option, echo;
!option, RBARC=FALSE;



/*****************************************************************************
 * LT
 * NB! The order of the .ele .str and .seq files matter.
 *     The reason is a >feature< of MADX
 *
 *****************************************************************************/
 call, file = '../strength/lt.str';
 call, file = '../elements/lt.ele';
 call, file = '../sequence/lt.seq';
 call, file = '../aperture/lt.dbx';



/*****************************************************************************
 * LTB
 * NB! The order of the .ele .str and .seq files matter.
 *     The reason is a >feature< of MADX
 *
 *****************************************************************************/
 call, file = '../strength/ltb.str';
 call, file = '../elements/ltb.ele';
 call, file = '../sequence/ltb.seq';
 call, file = '../aperture/ltb.dbx';



/*****************************************************************************
 * BI
 * NB! The order of the .ele .str and .seq files matter.
 *     The reason is a >feature< of MADX
 *
 *****************************************************************************/
 call, file = '../strength/bi.str';
 call, file = '../elements/bi.ele';
 call, file = '../sequence/BI1.seq';
 call, file = '../aperture/bi.dbx';



/*****************************************************************************
 * build up the geometry of the beam lines
 *****************************************************************************/
ltltbbi: sequence, refer=ENTRY,  l = 131.2644535262 ;
  lt                          , at =              0 ;
  ltb                         , at := 53.4728739515 ; ! 53.472856 + 17.9515 um ;
  bi1                         , at := 82.7080421462 ; ! 53.4728739515 + 29.2351681947. L=48.5564113800 + 82.7080421462   131.2644535262
endsequence;

!save, sequence=ltltbbi, file=ltltbbi1.save;



/*****************************************************************************
 * set initial twiss parameters
 *****************************************************************************/
call, file = '../inp/ltp.inp';



/*****************************************************************************
 * store initial parameters in memory block
 *****************************************************************************/
INITBETA0: BETA0,
  BETX=BETX0,
  ALFX=ALFX0,
  MUX=MUX0,
  BETY=BETY0,
  ALFY=ALFY0,
  MUY=MUY0,
  X=X0,
  PX=PX0,
  Y=Y0,
  PY=PY0,
  T=T0,
  PT=PT0,
  DX=DX0,
  DPX=DPX0,
  DY=DY0,
  DPY=DPY0;


/*******************************************************************************
 * Beam
 * NB! beam->ex == (beam->exn)/(beam->gamma*beam->beta*4)
 *******************************************************************************/
 Beam, particle=PROTON,pc=0.311,exn=(12.0E-6)*4.0,eyn=(6.0E-6)*4.0, sige=1E-3;



/*******************************************************************************
 * survey
 *******************************************************************************/
 a1 =             1.92035910512371099657 ;
 z0 =          1924.28279270772577547177 ;
 x0 =          1987.78747817067664982460 ;
 y0 =          2433.66                   ;
 set,  format="30.20f";

 sur1 : macro={
               use, sequence=ltltbbi;
               select, flag=survey,clear;
               select, flag=survey, column=name,s,l,z,x,y;
               survey, z0=z0, x0=x0, y0=y0
                     , theta0:=a1
                     , file="../out/ltltbbi1.sur";

               select, flag=survey, column=NAME,S,L,ANGLE,X,Y,Z,THETA,PHI,PSI,GLOBALTILT,SLOT_ID;
               survey, x0= -X0, y0= Y0, z0= Z0
                     , theta0= a1, phi0=  0.0000000000, psi0= 0.0000000000
                     , file="../out/ltltbbi1_input_for_GEODE.sur";
              }
 exec, sur1; ! NB: The result of the survey is in the file: "ltltbbi1.sur"

/*************************************
 * Note that the coordinates used by TS/SU
 * and in MADX are different:
 *      MADX    TS/SU
 *       Z        X
 *       X        Y
 *       Y        Z
 *************************************/



/*******************************************************************************
 * Use
 *******************************************************************************/
 use, sequence=ltltbbi;




/*******************************************************************************
 * twiss
 *******************************************************************************/

 maketwiss: macro=
 {
     ptc_create_universe;
     ptc_create_layout,model=2,method=6,nst=5,exact;
     ptc_twiss,table=ptc_twiss,BETA0=INITBETA0,icase=5,no=1; ! ,file="twiss";
     ptc_end;


 };

 exec, maketwiss;

!write,table=ptc_twiss;
!value,table(ptc_twiss,LTLTBBI$START,alfa11);
!value,table(ptc_twiss,LT.DHZ10,beta11);




/***************************************************
 * Write ptc_twiss table. NB! Values at end of elements
 ***************************************************/
set,  format="-18s";
set,  format="10.5f";

! ns    ~ longitudinal number of sigma
! nt    ~ transverse   number of sigma
! dimxc ~ size-x with dispersion, dimyc ~ size-y with dispersion,
! value, beam->ex, beam->ey, beam->sige;

 ns = 2;
 nt = 3;
 dimxc := sqrt( nt^2*table(ptc_twiss,beta11)*beam->ex + ns^2*(beam->sige*table(ptc_twiss,disp1))^2 );
 dimyc := sqrt( nt^2*table(ptc_twiss,beta22)*beam->ey + ns^2*(beam->sige*table(ptc_twiss,disp3))^2 );



 exec, maketwiss; ! Needs to be executed first, to make beta11, disp1, etc.
 select,flag=ptc_twiss,clear;
 select,flag=ptc_twiss, column = name,s,l,alfa11,beta11,disp1,dimxc,alfa22,beta22,dimyc;
 exec, maketwiss;
 write,table=ptc_twiss,file="../out/ltltbbi1.out";

 select,flag=ptc_twiss,clear;
 select,flag=ptc_twiss, column = keyword,name,parent,l,s;
 exec, maketwiss;
 write,table=ptc_twiss,file="../out/ltltbbi1_config.out";

 select,flag=ptc_twiss,clear;
 select,flag=ptc_twiss, column = name,angle,k1L,k2L,k3L,beta11,beta22,disp1,disp3,x,y,alfa11,alfa22,mu1,mu2,disp2,disp3,px,py;
 exec, maketwiss;
 write,table=ptc_twiss,file="../out/ltltbbi1_optics.out";




/*******************************************************************************
 * Plot ltltbbi
 *******************************************************************************/
option, -info;
option, -echo;

resplot;
setplot, post=2;

plot, title='bi'   , table=ptc_twiss
                   , haxis=s
                   , vaxis1=beta11,beta22
                   , style:=100,symbol:=4,colour=100
                   , file = "../out/ltltbbi1";

plot, title='bi'   , table=ptc_twiss
                   , haxis=s
                   , vaxis1=beta11,beta22
                   , vaxis2=disp1
                   , range=#S/#e
                   , style:=100, symbol:=4, colour=100;

plot, title='bi'   , table=ptc_twiss
                   , haxis=s
                   , vaxis1=disp1,disp3
                   , range=#S/#e
                   , style:=100, symbol:=4, colour=100;

/* Use >gv madx01.eps  to plot */


/*******************************************************************************
 * Clean up (remove Maxwellian_bend_for_ptc.txt )
 *******************************************************************************/

system, "rm Maxwellian_bend_for_ptc.txt";


stop;





