/*****************************************************
 *
 * MADX file for the LT-LTB line from LINAC2 to the LBE. This is a measurements line.
 *
 * Directory: /afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/
 *
 * Execute with:  >madx < lbs.madx
 * This file is for Protons at 311 MeV/c (Ekin=50 MeV, BRHO=1.03527 "the magnetic rigidity of our protons")
 *****************************************************/


/*****************************************************************************
 * TITLE
 *****************************************************************************/
 title, 'LT-LTB-LBS 2014 optics. Protons - 311 MeV/c (Ekin=50 MeV)';

 option, echo;
!option, RBARC=FALSE;

set,  format="-18s";
set,  format="12.5f";



/*****************************************************************************
 * LT (also called LTP line)
 * NB! The order of the .ele .str and .seq files matter.
 *     The reason is a >feature< of MADX
 *
 *****************************************************************************/
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/elements/lt.ele';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/sequence/lt.seq';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/strength/lt.str';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/aperture/lt.dbx';

/*****************************************************************************
 * LTB
 * NB! The order of the .ele .str and .seq files matter.
 *     The reason is a >feature< of MADX
 *
 *****************************************************************************/
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/elements/ltb.ele';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/sequence/ltb.seq';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/strength/ltb.str';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/aperture/ltb.dbx';



/*****************************************************************************
 * BI
 * NB! The order of the .ele .str and .seq files matter.
 *     The reason is a >feature< of MADX
 *
 *****************************************************************************/
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/elements/lbs.ele';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/sequence/lbs.seq';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/strength/lbs.str';




/*****************************************************************************
 * build up the geometry of the beam lines
 *****************************************************************************/

 ltltblbs: sequence, refer=ENTRY  ,  l =  98.713;
   lt                             , at =       000;
   ltb                            , at =  52.80100;
   lbs                            , at =  52.80100+29.24103;
 endsequence;

!save, sequence=ltltblbs, file=ltltblbs.save;




/*****************************************************************************
 * set initial twiss parameters
 *****************************************************************************/
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/inp/ltp.inp';



/*****************************************************************************
 * store initial parameters in memory block
 *****************************************************************************/
INITBETA0: BETA0,
  BETX=BETX0,
  ALFX=ALFX0,
  MUX=MUX0,
  BETY=BETY0,
  ALFY=ALFY0,
  MUY=MUY0,
  X=X0,
  PX=PX0,
  Y=Y0,
  PY=PY0,
  T=T0,
  PT=PT0,
  DX=DX0,
  DPX=DPX0,
  DY=DY0,
  DPY=DPY0;


/*******************************************************************************
 * Beam
 * NB! beam->ex == (beam->exn)/(beam->gamma*beam->beta*4)
 *******************************************************************************/
 Beam, particle=PROTON,pc=0.311,exn=(12.0E-6)*4.0,eyn=(6.0E-6)*4.0, sige=1E-3;



/*******************************************************************************
 * survey
 *******************************************************************************/
 a1 =    1.920361068461749 ; ! This value is for the lt line
 sur1 : macro={

        use, sequence=ltltblbs;
        select, flag=survey,clear;
        select, flag=survey, column=name,s,l,z,x,y;
        survey, z0=1924.28289, x0=1987.78721, y0=2433.66 ! Start point of VVS10

              , theta0:=a1
              , file="../out/ltltblbs.sur";
              }
 exec, sur1; ! NB: The result of the survey is in the file: "ltltblbs.sur"
!stop;

/*************************************
 * Find the initial angle of the beam.
 * The angle is: a1=2.63876699
 * Note that the coordinates used by TS/SU
 * and in MADX are different:
 *      MADX    TS/SU
 *       Z        X
 *       X        Y
 *       Y        Z
 *************************************/

!match, use_macro;
!   vary, name=angle_LBS_BVT10;
!   vary, name=dd;
!   vary, name=dd1;
!   vary, name=dd2;
!
!   use_macro,name=sur1;
!
!   constraint, expr=table(survey,LBS.MSGV50,Z)=1895.81855000;
!   constraint, expr=table(survey,LBS.MSGV50,X)=2079.30045000;
!   constraint, expr=table(survey,LBS.MSGV50,Y)=2436.83299500;
!
!   lmdif, calls=1000, tolerance=6E-28;
!endmatch;
!
!exec, sur1;
! // write, table=survey;
!value, dd;
!value, table(survey,LBS.BHZ40,Z);
!stop;
 /*************************************/



/*******************************************************************************
 * Use
 *******************************************************************************/
 use, sequence=ltltblbs;



/*******************************************************************************
 * twiss
 *******************************************************************************/

 maketwiss: macro=
 {
     ptc_create_universe;
     ptc_create_layout,model=2,method=6,nst=5,exact;
     ptc_twiss,table=ptc_twiss,BETA0=INITBETA0,icase=5,no=1; ! ,file="twiss";
     ptc_end;


};

 exec, maketwiss;

!write,table=ptc_twiss;
!value,table(ptc_twiss,LTLTBLBS$START,alfa11);
!value,table(ptc_twiss,LT.DHZ10,beta11);




/***************************************************
 * Write ptc_twiss table. NB! Values at end of elements
 ***************************************************/
 set,  format="-18s";
 set,  format="12.5f";

! ns    ~ longitudinal number of sigma
! nt    ~ transverse   number of sigma
! dimxc ~ size-x with dispersion, dimyc ~ size-y with dispersion,
! value, beam->ex, beam->ey, beam->sige;

 ns = 2;
 nt = 3;
 dimxc := sqrt( nt^2*table(ptc_twiss,beta11)*beam->ex + ns^2*(beam->sige*table(ptc_twiss,disp1))^2 );
 dimyc := sqrt( nt^2*table(ptc_twiss,beta22)*beam->ey + ns^2*(beam->sige*table(ptc_twiss,disp3))^2 );



 exec, maketwiss; ! Needs to be executed first, to make beta11, disp1, etc.
 select,flag=ptc_twiss,clear;
 select,flag=ptc_twiss, column = name,s,l,alfa11,beta11,disp1,dimxc,alfa22,beta22,disp3,dimyc;
 exec, maketwiss;

 write,table=ptc_twiss,file="../out/optics_lbs.out";










/*******************************************************************************
 * Plot ltltblbs
 *******************************************************************************/
 option, -info;
 option, -echo;

 resplot;
 setplot, post=2;

 plot, title='lbs'  , table=ptc_twiss
                    , haxis=s
                    , vaxis1=beta11,beta22
                    , style:=100,symbol:=4,colour=100
                    , file = "../out/lbs";

 plot, title='Twiss', table=ptc_twiss
                    , haxis=s
                    , vaxis1=beta11,beta22
                    , vaxis2=disp1
                    , range=#S/#e
                    , style:=100, symbol:=4, colour=100;

 plot, title='Twiss', table=ptc_twiss
                    , haxis=s
                    , vaxis1=disp1,disp3
                    , range=#S/#e
                    , style:=100, symbol:=4, colour=100;

/* Use >gv lbs01.eps  to plot */


 stop;







