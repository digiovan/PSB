/*****************************************************
 *
 * MADX file for the LT-LTB line from LINAC2 to the PS BOOSTER
 *
 * Directory: /afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/
 *

 * Execute with:  >madx < ltltbbi1.madx
 * This file is for Protons at 310.3662354  MeV/c (Ekin=50 MeV, BRHO=1.0352576 "the magnetic rigidity of the protons")
 *****************************************************/


/*****************************************************************************
 * TITLE
 *****************************************************************************/
 title, 'LT-LTB-BI 2014 optics. Protons - 311 MeV/c (E_kin=50 MeV)';

 option, echo;
!option, RBARC=FALSE;



/*****************************************************************************
 * LT
 * NB! The order of the .ele .str and .seq files matter.
 *     The reason is a >feature< of MADX
 *
 *****************************************************************************/
 call, file = '../../strength/lt.str';
 call, file = '../../elements/lt.ele';
 call, file = '../../sequence/lt.seq';
 call, file = '../../aperture/lt.dbx';



/*****************************************************************************
 * LTB
 * NB! The order of the .ele .str and .seq files matter.
 *     The reason is a >feature< of MADX
 *
 *****************************************************************************/
 call, file = '../../strength/ltb.str';
 call, file = '../../elements/ltb.ele';
 call, file = '../../sequence/ltb.seq';
 call, file = '../../aperture/ltb.dbx';



/*****************************************************************************
 * BI
 * NB! The order of the .ele .str and .seq files matter.
 *     The reason is a >feature< of MADX
 *
 *****************************************************************************/
 call, file = '../../strength/bi.str';
 call, file = '../../elements/bi.ele';
 call, file = '../../sequence/BI1.seq';
 call, file = '../../aperture/bi.dbx';



/*****************************************************************************
 * build up the geometry of the beam lines
 *****************************************************************************/
ltltbbi: sequence, refer=ENTRY,  l = 131.2644535262 ;
  lt                          , at =              0 ;
  ltb                         , at := 53.4728739515 ; ! 53.472856 + 17.9515 um ;
  bi1                         , at := 82.7080421462 ; ! 53.4728739515 + 29.2351681947. L=48.5564113800 + 82.7080421462   131.2644535262
endsequence;

!save, sequence=ltltbbi, file=ltltbbi1.save;



/*****************************************************************************
 * set initial twiss parameters
 *****************************************************************************/
call, file = '../../inp/ltp.inp';



/*****************************************************************************
 * store initial parameters in memory block
 *****************************************************************************/
INITBETA0: BETA0,
  BETX=BETX0,
  ALFX=ALFX0,
  MUX=MUX0,
  BETY=BETY0,
  ALFY=ALFY0,
  MUY=MUY0,
  X=X0,
  PX=PX0,
  Y=Y0,
  PY=PY0,
  T=T0,
  PT=PT0,
  DX=DX0,
  DPX=DPX0,
  DY=DY0,
  DPY=DPY0;


/*******************************************************************************
 * Beam
 * NB! beam->ex == (beam->exn)/(beam->gamma*beam->beta*4)
 *******************************************************************************/
 Beam, particle=PROTON,pc=0.3103662354,exn=(12.0E-6)*4.0,eyn=(6.0E-6)*4.0, sige=1E-3;


/*******************************************************************************
 * Use
 *******************************************************************************/
 use, sequence=ltltbbi;



/*******************************************************************************
 * twiss
 *******************************************************************************/
 maketwiss: macro=
 {
     assign, echo="DVT30_QNO30_QNO40.str"; value, dBIDVT30,KBI.QNO30,KBI.QNO40; assign, echo=terminal;  ! Contains KBI.QNO40 strength
     system, "madx < match_QNO30.madx > /dev/null";  ! Calulates the strength of the QNO30
     call, file="DVT30_QNO30_QNO40.str";

     ptc_create_universe;
     ptc_create_layout,model=2,method=6,nst=5,exact;
     ptc_twiss,table=ptc_twiss,BETA0=INITBETA0,icase=5,no=2; ! ,file="twiss";
     ptc_end;


     value, dBIDVT30   ;
     value, KBI.QNO10  ;
     value, KBI.QNO20  ;
     value, KBI.QNO30  ;
     value, KBI.QNO40  ;
     value, KBI1.QNO50 ;
     value, KBI1.QNO60 ;


     value, table(ptc_twiss,BI1.INJPT,BETA11) ;
     value, table(ptc_twiss,BI1.INJPT,ALFA11) ;
     value, table(ptc_twiss,BI1.INJPT,BETA22) ;
     value, table(ptc_twiss,BI1.INJPT,ALFA22) ;
     value, table(ptc_twiss,BI1.INJPT,DISP1)  ;
     value, table(ptc_twiss,BI1.INJPT,DISP2)  ;
     value, table(ptc_twiss,BI1.INJPT,DISP3)  ;
     value, table(ptc_twiss,BI1.INJPT,DISP4)  ;

 };

 exec, maketwiss;

!write,table=ptc_twiss;
!value,table(ptc_twiss,LTLTBBI$START,alfa11);
!value,table(ptc_twiss,LT.DHZ10,beta11);




/*******************************************************************************
 * matching
 *******************************************************************************/
option, info;
option, echo;

 match,use_macro;

!
! vary LT quads (starting after first bending)
!
!         vary, name=KLT.QFN10, step=1E-4;
!         vary, name=KLT.QDN12, STEP=1E-4;
!         vary, name=KLT.QFN20, STEP=1E-4;
!         vary, name=KLT.QDN22, STEP=1E-4;
!         vary, name=KLT.QFN30, STEP=1E-4;
!         vary, name=KLT.QDN32, step=1E-4;
!         vary, name=KLT.QFN40, STEP=1E-4;
!         vary, name=KLT.QDN42, STEP=1E-4;
!         vary, name=KLT.QFN50, STEP=1E-4;
!         vary, name=KLT.QDN55, STEP=1E-4;
!         vary, name=KLT.QFN60, STEP=1E-4;
!         vary, name=KLT.QFW70, STEP=1E-4;
!         vary, name=KLT.QDN75, STEP=1E-4;
!
! vary LTB quads
!
!         vary, name=KLTB.QFN10, STEP=1E-4;
!         vary, name=KLTB.QDN20, STEP=1E-4;
!         vary, name=KLTB.QDW40, STEP=1E-4;
!         vary, name=KLTB.QFW50, STEP=1E-4;
!         vary, name=KLTB.QDW60, STEP=1E-4;
!
! vary BI quads

          vary, name=KBI.QNO10,  STEP=1E-4;
          vary, name=KBI.QNO20,  STEP=1E-4;
!         vary, name=KBI.QNO30,  STEP=1E-4;
          vary, name=KBI.QNO40,  STEP=1E-4;
          vary, name=KBI1.QNO50, STEP=1E-4;
          vary, name=KBI1.QNO60, STEP=1E-4;

    use_macro,name=maketwiss;

! constraints at PSB injection point
           constraint, expr=    table(ptc_twiss,BI1.INJPT,BETA11)       =  5.6;
           constraint, expr=abs(table(ptc_twiss,BI1.INJPT,ALFA11))      =    0;
           constraint, expr=    table(ptc_twiss,BI1.INJPT,BETA22)       = 4.22;
           constraint, expr=abs(table(ptc_twiss,BI1.INJPT,ALFA22))      =    0;
           constraint, expr=    table(ptc_twiss,BI1.INJPT,DISP1)*1000   =-1.42*1000;  ! dx
           constraint, expr=abs(table(ptc_twiss,BI1.INJPT,DISP2))       =    0;       ! dpx
           constraint, expr=abs(table(ptc_twiss,BI1.INJPT,DISP3))       =    0;       ! dy
           constraint, expr=abs(table(ptc_twiss,BI1.INJPT,DISP4))       =    0;       ! dpy

!          constraint, range=#S/BI1.INJPT, BETA11<80, BETA22<50;

!   lmdif, calls = 500, tolerance = 1.0E-21;
!   simplex, calls = 1000, tolerance = 1.0E-25;
    lmdif, calls = 5000, tolerance = 1.0E-5;
 endmatch;

 option, info;
 option, echo;

 set,  format="-18s";
 set,  format="10.5f";
 value, angleDVT30 ;  print, text=" was  DBIDVT30   =  0.001687693791752 ;";
 value, KBI.QNO10  ;  print, text=" was  KBI.QNO10  =  1.12452 ;" ;
 value, KBI.QNO20  ;  print, text=" was  KBI.QNO20  = -1.29490 ;" ;
 value, KBI.QNO30  ;  print, text=" was  KBI.QNO30  =  0.72923 ;" ;
 value, KBI.QNO40  ;  print, text=" was  KBI.QNO40  = -0.98140 ;" ;
 value, KBI1.QNO50 ;  print, text=" was  KBI1.QNO50 =  1.13646 ;" ;
 value, KBI1.QNO60 ;  print, text=" was  KBI1.QNO60 = -1.29881 ;" ;

!stop;



 /*******************************************************************************/





/***************************************************
 * Write ptc_twiss table. NB! Values at end of elements
 ***************************************************/
 set,  format="-18s";
 set,  format="10.5f";

! ns    ~ longitudinal number of sigma
! nt    ~ transverse   number of sigma
! dimxc ~ size-x with dispersion, dimyc ~ size-y with dispersion,
! value, beam->ex, beam->ey, beam->sige;

 ns = 2;
 nt = 3;
 dimxc := sqrt( nt^2*table(ptc_twiss,beta11)*beam->ex + ns^2*(beam->sige*table(ptc_twiss,disp1))^2 );
 dimyc := sqrt( nt^2*table(ptc_twiss,beta22)*beam->ey + ns^2*(beam->sige*table(ptc_twiss,disp3))^2 );



 select,flag=ptc_twiss,clear;
 select,flag=ptc_twiss, column = name,s,beta11,beta22,disp1,disp2,alfa11,alfa22,disp3,disp4,dimxc,dimyc;

 exec, maketwiss;
 write,table=ptc_twiss,file="ltltbbi1_optics.out";




/*******************************************************************************
 * Plot ltltbbi
 *******************************************************************************/
 option, -info;
 option, -echo;

 resplot;
 setplot, post=2;

 plot, title='bi'   , table=ptc_twiss
                    , haxis=s
                    , vaxis1=beta11,beta22
                    , style:=100,symbol:=4,colour=100
                    , file = "ltltbbi1";

 plot, title='bi'   , table=ptc_twiss
                    , haxis=s
                    , vaxis1=beta11,beta22
                    , vaxis2=disp1
                    , range=#S/#e
                    , style:=100, symbol:=4, colour=100;

 plot, title='bi'   , table=ptc_twiss
                    , haxis=s
                    , vaxis1=disp1,disp3
                    , range=#S/#e
                    , style:=100, symbol:=4, colour=100;

 /* Use >gv madx01.eps  to plot */




/*******************************************************************************
 * survey
 *******************************************************************************/
 a1 =             1.92035910512371099657 ;
 z0 =          1924.28279270772577547177 ;
 x0 =          1987.78747817067664982460 ;
 y0 =          2433.66                   ;
 set,  format="30.20f";

 sur1 : macro={
               use, sequence=ltltbbi;
               select, flag=survey,clear;
               select, flag=survey, column=name,s,l,z,x,y;
               survey, z0=z0, x0=x0, y0=y0
                     , theta0:=a1
                     , file="ltltbbi1.sur";
              }
 exec, sur1; ! NB: The result of the survey is in the file: "ltltbbi1.sur"

/*************************************
 * Note that the coordinates used by TS/SU
 * and in MADX are different:
 *      MADX    TS/SU
 *       Z        X
 *       X        Y
 *       Y        Z
 *************************************/



/*******************************************************************************
 * Clean up (remove Maxwellian_bend_for_ptc.txt )
 *******************************************************************************/

 system, "rm Maxwellian_bend_for_ptc.txt";


 stop;





