/*****************************************************
 *
 * MADX file for the LT-LTB line from LINAC2 to the PS BOOSTER
 *
 * Directory: /afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2011/
 *
 * Execute with:  >madx < bi.madx
 * This file is for Protons at 311 MeV/c (Ekin=50 MeV, BRHO=1.03527 "the magnetic rigidity of our protons")
 *****************************************************/


/*****************************************************************************
 * TITLE
 *****************************************************************************/
 title, 'LT-LTB-BI 2011 optics. Protons - 311 MeV/c (Ekin=50 MeV)';

 option, echo;
!option, RBARC=FALSE;




/*****************************************************************************
 * LT (also called LTP line)
 * NB! The order of the .ele .str and .seq files matter.
 *     The reason is a >feature< of MADX
 *
 *****************************************************************************/
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2011/elements/lt.ele';



 LT.DHZ10         : HKICKER,         L= 0, KICK=0.0005; ! Real length L= 0.295


 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2011/sequence/lt.seq';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/strength/lt.str';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2011/aperture/lt.dbx';

/*****************************************************************************
 * LTB
 * NB! The order of the .ele .str and .seq files matter.
 *     The reason is a >feature< of MADX
 *
 *****************************************************************************/
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2011/elements/ltb.ele';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2011/sequence/ltb.seq';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2014/strength/ltb.str';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2011/aperture/ltb.dbx';



/*****************************************************************************
 * BI
 * NB! The order of the .ele .str and .seq files matter.
 *     The reason is a >feature< of MADX
 *
 *****************************************************************************/
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2011/elements/bi.ele';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2011/sequence/bi.seq';
 call, file = 'bi.str';
 call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2011/aperture/bi.dbx';



/*****************************************************************************
 * build up the geometry of the beam lines
 *****************************************************************************/
ltltbbi: sequence, refer=ENTRY  ,  l = 131.20453;
  lt                            , at =       000;
  ltb                           , at =  53.5479640080;
  bi                            , at =  53.5479640080+29.16182067;
endsequence;

!save, sequence=ltltbbi, file=ltltbbi.save;




/*****************************************************************************
 * set initial twiss parameters
 *****************************************************************************/
call, file = '/afs/cern.ch/eng/ps/cps/TransLines/Linac2-PSB/2011/inp/ltp.inp';



/*****************************************************************************
 * store initial parameters in memory block
 *****************************************************************************/
INITBETA0: BETA0,
  BETX=BETX0,
  ALFX=ALFX0,
  MUX=MUX0,
  BETY=BETY0,
  ALFY=ALFY0,
  MUY=MUY0,
  X=X0,
  PX=PX0,
  Y=Y0,
  PY=PY0,
  T=T0,
  PT=PT0,
  DX=DX0,
  DPX=DPX0,
  DY=DY0,
  DPY=DPY0;


/*******************************************************************************
 * Beam
 * NB! beam->ex == (beam->exn)/(beam->gamma*beam->beta*4)
 *******************************************************************************/
 Beam, particle=PROTON,pc=0.311,exn=(12.0E-6)*4.0,eyn=(6.0E-6)*4.0, sige=1E-3;

 set,  format="20.15f";




/*******************************************************************************
 * Use
 *******************************************************************************/
 use, sequence=ltltbbi;








/*******************************************************************************
 * twiss
 *******************************************************************************/

 maketwiss: macro=
 {
     ptc_create_universe;
     ptc_create_layout,model=2,method=6,nst=5,exact;
     ptc_twiss,table=ptc_twiss,BETA0=INITBETA0,icase=5,no=1; ! ,file="twiss";
     ptc_end;


};

 exec, maketwiss;

!write,table=ptc_twiss;
!value,table(ptc_twiss,LTLTBBI$START,alfa11);
!value,table(ptc_twiss,LT.DHZ10,beta11);




/***************************************************
 * Write ptc_twiss table. NB! Values at end of elements
 ***************************************************/
set,  format="-18s";
set,  format="10.5f";

! ns    ~ longitudinal number of sigma
! nt    ~ transverse   number of sigma
! dimxc ~ size-x with dispersion, dimyc ~ size-y with dispersion,
! value, beam->ex, beam->ey, beam->sige;

 ns = 2;
 nt = 3;
 dimxc := sqrt( nt^2*table(ptc_twiss,beta11)*beam->ex + ns^2*(beam->sige*table(ptc_twiss,disp1))^2 );
 dimyc := sqrt( nt^2*table(ptc_twiss,beta22)*beam->ey + ns^2*(beam->sige*table(ptc_twiss,disp3))^2 );



 exec, maketwiss; ! Needs to be executed first, to make beta11, disp1, etc.
 select,flag=ptc_twiss,clear;
 select,flag=ptc_twiss, column = name,s,angle,k1L,k2L,k3L,x,y,px,py,alfa11,alfa22,beta11,beta22;
 exec, maketwiss;

write,table=ptc_twiss,file="ltltbbi_optics_2011.out";










/*******************************************************************************
 * Plot ltltbbi
 *******************************************************************************/
option, -info;
option, -echo;

resplot;
setplot, post=2;

plot, title='ltltbbi2011'   , table=ptc_twiss
                   , haxis=s
                   , vaxis1=beta11,beta22
                   , style:=100,symbol:=4,colour=100
                   , file = "ltltbbi2011";

plot, title='Twiss', table=ptc_twiss
                   , haxis=s
                   , vaxis1=beta11,beta22
                   , vaxis2=disp1
                   , range=#S/#e
                   , style:=100, symbol:=4, colour=100;

plot, title='Twiss', table=ptc_twiss
                   , haxis=s
                   , vaxis1=disp1,disp3
                   , range=#S/#e
                   , style:=100, symbol:=4, colour=100;

/* Use >gv madx01.eps  to plot */



/*******************************************************************************
 * Clean up (remove Maxwellian_bend_for_ptc.txt )
 *******************************************************************************/

system, "rm Maxwellian_bend_for_ptc.txt";




stop;







