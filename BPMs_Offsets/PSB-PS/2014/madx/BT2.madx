!--------------------------------------------------------------------------
! Optics file to calculate the optics of the transferline BT2. The trajectory is
! based on the report PS/OP/BR/Note 81-5 from J.P. Delahaye and J.P. Rinaud.
!
!
! - BT1 recombines totaly with BT2 after BT1.KF10 and with BT3 after BT2.KFA20.
! - The QNO10, QNO20 and QN30 are misaligned.
!
! This file is for protons with energy of 1.4 GeV and momentum of 2 GeV/c
!--------------------------------------------------------------------------
! Directory: /afs/cern.ch/eng/ps/cps/TransLines/PSB-PS/2013
! Optics file created in January 2013 by V.Raginel, O.Berrig and B. Mikulec
!--------------------------------------------------------------------------

 title, 'Transferline BT2 optics. Protons - 1.4 GeV';

 set,  format="-18s";
 set,  format="10.6f";

/******************************************************************************************
 * BT
 ******************************************************************************************/
 option, echo;

 call, file = '../strength/BT_BTP.str';
!call, file = '../strength/BT_dump.str';
!call, file = '../strength/BT_hor_emit_largeDx.str';
!call, file = '../strength/BT_hor_emit_smallDx.str';
!call, file = '../strength/BTP.str  BT_ver_emit.str';
 call, file = '../elements/BT.ele';
 call, file = '../sequence/BT2.seq';
 call, file = '../aperture/BT.dbx';


/******************************************************************************************
 * set initial twiss parameters
 ******************************************************************************************/
 call, file = '../inp/BT.inp';

!-----------------------------------------------------
! set initial position and angle (x,px) and (y,py)
! E.g. :
! x0 := 0.0;
! px0:= 0.0;
! y0 := 0.0;
! py0:= 0.0;
!-----------------------------------------------------

 INITBETA0:  BETA0,
      BETX = BETX0,
      ALFX = ALFX0,
      MUX  = MUX0,
      BETY = BETY0,
      ALFY = ALFY0,
      MUY  = MUY0,
      X    = X0,
      PX   = PX0,
      Y    = Y0,
      PY   = PY0,
      T    = T0,
      PT   = PT0,
      DX   = DX0,
      DPX  = DPX0,
      DY   = DY0,
      DPY  = DPY0;


/******************************************************************************************
 * beam and use
 ******************************************************************************************/
 beam, particle=PROTON, pc=2.14;
 use, sequence=BT2;




/******************************************************************************************
 ******************************************************************************************
 *
 * PTC OPTICS for BT2sequence
 *
 ******************************************************************************************
 ******************************************************************************************/
 select,flag=ptc_twiss,clear;
 select,flag=ptc_twiss, column =name, keyword, s, l, k0l, k1l,angle, y, py, x, px, BETX, ALFX, BETY, ALFY, DISP1, DISP2, DISP3,DISP4, MU1, MU2;

 ptc_create_universe;
 ptc_create_layout,time=false,model=2,method=6,nst=5,exact,closed_layout=false;
 ptc_twiss,table=ptc_twiss,BETA0=INITBETA0, icase=5,no=2;
 write,table=ptc_twiss,file="../out/BT2.out";
 ptc_end;


/******************************************************************************************
 * Print out definitions of elements
 ******************************************************************************************/
 show, BT2.DVT10;
 show, BT2.DVT20;
 show, BT2.QNO10;
 show, BT2.QNO20;
 show, BT2.BVT20;
 show, BT2.SMV20;
 show, BT2.QNO30;
 show, BT2.KFA20;


/******************************************************************************************
 * Save optics functions at the end of the BT2 line in the file "../inp/BTP2.inp"
 ******************************************************************************************/
 BETX0 := table(ptc_twiss,BT2.END,beta11);
 ALFX0 := table(ptc_twiss,BT2.END,alfa11);
 BETY0 := table(ptc_twiss,BT2.END,beta22);
 ALFY0 := table(ptc_twiss,BT2.END,alfa22);
 DX0   := table(ptc_twiss,BT2.END,disp1);
 DPX0  := table(ptc_twiss,BT2.END,disp2);


/******************************************************************************************
 *  save in "../inp/BTP2inp"
 ******************************************************************************************/
!system, "rm ../inp/BTP2.inp";
!assign, echo="../inp/BTP2.inp";
 print, text="! BTP2 input, automatically generated from BT2.out file (i.e. ring 2) with BTP optics";
 print, text="! Optics function at the entry flange of the BT.BHZ10";
 value,  BETX0;
 value,  ALFX0;
 value,  BETY0;
 value,  ALFY0;
 value,  DX0  ;
 value,  DPX0 ;
 assign, echo=terminal;


/******************************************************************************************
 * PLOT
 ******************************************************************************************/
 resplot;
 setplot, post=2;

 plot, title='BT2'  , table=ptc_twiss
                    , haxis=s
                    , vaxis1=beta11,beta22
                    , style:=100,symbol:=4,colour=100
                    , file = "../out/BT2";


/******************************************************************************************
 * Survey
 ******************************************************************************************/
 select, flag=survey, clear;
 select, flag=survey, column=name, s, l, z, x, y;

! New Ejection point given by Drawings and Survey z0=1880.1405971066347, x0=2090.7822669027646
! Point A(xA,yA) is the ejection point of Survey and Point B(xB,yB) is the deflection point in BT.BHZ10 given by Survey.
! Point PSBex (xPSBex, yPSBex) is the new BT start point
! xPSBex =  xA - (34.9976 - Sqrt[((xB - xA)^2 + (yB - yA)^2)])*(xB - xA)/Sqrt[((xB - xA)^2 + (yB - yA)^2)]
! yPSBex = yA - (34.9976 - Sqrt[((xB - xA)^2 + (yB - yA)^2)])*(yB - yA)/ Sqrt[((xB - xA)^2 + (yB - yA)^2)]

! New start of BT line, exit flange of the SMH15L1
! z0=1880.1405971066347, x0=2090.7822669027646  and  theta0 = 7.404510501918276 (GEODE 2007)

 survey, z0=1880.1405971066347, x0=2090.7822669027646, y0=2433.66-0.36, theta0=7.404510501918276, file="../out/BT2.survey";

 plot, title='BT2_survey', table=survey, vaxis=Y,haxis=Z, SPLINE=FALSE, COLOUR=100, RANGE=#S/#E, file = "../out/BT2";

/******************************************************************************************
 * remove: Maxwellian_bend_for_ptc.txt
 ******************************************************************************************/
 system,"rm Maxwellian_bend_for_ptc.txt";

 stop;



