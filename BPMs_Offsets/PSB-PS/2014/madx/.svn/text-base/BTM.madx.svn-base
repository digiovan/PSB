!--------------------------------------------------------------------------
! MADX file to calculate the optics of the transferline BTM dump. The trajectory is
! based on the report PS/OP/BR/Note 81-5 from J.P. Delahaye and J.P. Rinaud.
!
! - BT3 is a straight line.
! - The QNO10, QNO20 and QN30 are misaligned.
! - The vertical correctors DVT10, DVT20, DVT30, DVT40 are used as bending
!   to compensate the misalignment of the QNO.
!
! Directory: /afs/cern.ch/eng/ps/cps/TransLines/PSB-PS/2013/madx
! Madx file created in August 2012 by V.Raginel, O.Berrig and B. Mikulec
!
! This file is for protons with energy of 1.4 GeV and momentum of 2 GeV/c
!--------------------------------------------------------------------------


 set,  format="-18s";
 set,  format="10.6f";


/******************************************************************************************
 * BTM
 ******************************************************************************************/
 option, echo;

 call, file = '../strength/BTM_dump.str';
 call, file = '../elements/BTM.ele';
 call, file = '../sequence/BTM.seq';

!save, sequence=btm, file='BTM.seq', bare;


/******************************************************************************************
 * set initial twiss parameters
 ******************************************************************************************/
 call, file = '../inp/BTM3_dump.inp';

!-----------------------------------------------------
! set initial position and angle (x,px) and (y,py)
! E.g. :
! x0 := 0.0;
! px0:= 0.0;
! y0 := 0.0;
! py0:= 0.0;
!-----------------------------------------------------

 INITBETA0:  BETA0,
      BETX = BETX0,
      ALFX = ALFX0,
      MUX  = MUX0,
      BETY = BETY0,
      ALFY = ALFY0,
      MUY  = MUY0,
      X    = X0,
      PX   = PX0,
      Y    = Y0,
      PY   = PY0,
      T    = T0,
      PT   = PT0,
      DX   = DX0,
      DPX  = DPX0,
      DY   = DY0,
      DPY  = DPY0;


/******************************************************************************************
 * beam and use
 ******************************************************************************************/
 beam, particle=PROTON, pc=2.14;
 use, sequence=btm;


/******************************************************************************************
 ******************************************************************************************
 *
 * PTC OPTICS for BTM sequence
 *
 ******************************************************************************************
 ******************************************************************************************/
 select,flag=ptc_twiss,clear;
 select,flag=ptc_twiss, column =name,s,x,px,y,py,alfa11,beta11,alfa22,beta22,mu1,mu2,disp1,disp2;

 ptc_create_universe;
 ptc_create_layout,time=false,model=2,method=6,nst=5,exact,closed_layout=false;
 ptc_twiss,table=ptc_twiss,BETA0=INITBETA0, icase=5,no=2;
 write,table=ptc_twiss,file="../out/BTM.out";
 ptc_end;


/******************************************************************************************
 * PLOT
 ******************************************************************************************/
 resplot;
 setplot, post=2;

 PLOT, title='BTM'  , table=ptc_twiss
                    , haxis=s
                    , vaxis1=beta11,beta22
                    , style:=100,symbol:=4,colour=100
                    , file = "../out/BTM";

 PLOT, title='BTM'  , table=ptc_twiss
                    , haxis=s
                    , vaxis1=mu1,mu2
                    , style:=100,symbol:=4,colour=100
                    , file="../out/BTM";




/******************************************************************************************
 * Survey
 ******************************************************************************************/
 select, flag=survey, clear;
 select, flag=survey, column=name, s, l, z, x, y;

! Start of BTM line, entry flange of the BT.BHZ10
! z0=1895.012875308754474, x0=2121.611842025202805  and  theta0 = 7.404510501918276 (GEODE 2007)

 survey, z0=1895.012875308754474, x0=2121.611842025202805, y0=2433.66, theta0=7.404510501918276, file="../out/BTM.survey";

 plot, table=survey, vaxis=Y,haxis=Z, SPLINE=FALSE, COLOUR=100, RANGE=#S/#E, file = "../out/BTM3";


/******************************************************************************************
 * remove: Maxwellian_bend_for_ptc.txt
 ******************************************************************************************/
 system,"rm Maxwellian_bend_for_ptc.txt";

 stop;
